plugins {
    id("fabric-loom") version "1.10.0-bta"
    id("java")
}

import org.gradle.internal.os.OperatingSystem
project.ext.lwjglVersion = "3.3.3"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
}

group = project.mod_group
archivesBaseName = project.mod_name
version = project.mod_version

loom {
    noIntermediateMappings()
    customMinecraftMetadata.set("https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/v${project.bta_version}/manifest.json")
    accessWidenerPath = file("src/main/resources/proxvc.accesswidener")
}

repositories {
    mavenCentral()
    ivy {
        name = "Minecraft"
        url = "https://piston-data.mojang.com"
        patternLayout {
            artifact "v1/[organisation]/[revision]/[module].jar"
            m2compatible = true
        }
        metadataSources {
            artifact()
        }
    }
    ivy {
        name = "BTA"
        url = "https://github.com/Better-than-Adventure"
        patternLayout {
            artifact "[organisation]/releases/download/v[revision]/[module].jar"
            m2compatible = true
        }
        metadataSources {
            artifact()
        }
    }
    maven {
        name = "Babric"
        url = "https://maven.glass-launcher.net/babric"
    }
    ivy {
        name = "Fabric Loader, HalpLibe, Mod Menu"
        url = "https://github.com/Turnip-Labs"
        patternLayout {
            artifact "[organisation]/releases/download/[revision]/[module]-[revision].jar"
            m2compatible = true
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    minecraft "bta-download-repo:bta:${project.bta_version}"
    mappings loom.layered() {}

    // https://piston-data.mojang.com/v1/objects/43db9b498cb67058d2e12d394e6507722e71bb45/client.jar
    modRuntimeOnly "objects:client:43db9b498cb67058d2e12d394e6507722e71bb45"
    modImplementation "fabric-loader:fabric-loader:${project.loader_version}"
    implementation "org.apache.logging.log4j:log4j-core:2.20.0"

    modRuntimeOnly "bta-halplibe:halplibe:${project.halplibe_version}"
    implementation "org.apache.logging.log4j:log4j-1.2-api:2.20.0"
    implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.16.0"
    implementation "com.google.guava:guava:33.0.0-jre"

    modRuntimeOnly "ModMenu:ModMenu-bta:${project.mod_menu_version}"

    implementation(platform("org.lwjgl:lwjgl-bom:$lwjglVersion"))

    runtimeOnly("org.lwjgl:lwjgl::$lwjglNatives")
    runtimeOnly("org.lwjgl:lwjgl-assimp::$lwjglNatives")
    runtimeOnly("org.lwjgl:lwjgl-glfw::$lwjglNatives")
    runtimeOnly("org.lwjgl:lwjgl-openal::$lwjglNatives")
    runtimeOnly("org.lwjgl:lwjgl-opengl::$lwjglNatives")
    runtimeOnly("org.lwjgl:lwjgl-stb::$lwjglNatives")
    implementation("org.lwjgl:lwjgl:$lwjglVersion")
    implementation("org.lwjgl:lwjgl-assimp:$lwjglVersion")
    implementation("org.lwjgl:lwjgl-glfw:$lwjglVersion")
    implementation("org.lwjgl:lwjgl-openal:$lwjglVersion")
    implementation("org.lwjgl:lwjgl-opengl:$lwjglVersion")
    implementation("org.lwjgl:lwjgl-stb:$lwjglVersion")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release.set 8
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

configurations.configureEach {
    // Removes LWJGL2 dependencies.
    exclude group: "org.lwjgl.lwjgl"
}

processResources {
    inputs.property "mod_version", project.mod_version
    filesMatching("fabric.mod.json") {
        expand "mod_version": project.mod_version
    }
}
